*** Settings ***
Documentation     This resource file covers all reusable actions for Recap Panel related test cases
Variables         ../variables/recap_control_objects.py
Resource          ../common/core.txt

*** Keywords ***
Click Add Queue Minder
    Click Control Button    [NAME:cmdAddQueueMinder]

Click Finish PNR
    [Arguments]    ${keyword}=${EMPTY}    ${delay_in_secs}=5    ${panel}=Recap
    Run Keyword If    '${gds_switch}' == 'sabre'    Enter Specific Command On Native GDS    W-ABC TRAVEL¥W-1/123 MAIN ST¥W-2/DALLAS TX 75201    5/
    Click Panel    ${panel}
    Click Control Button    [NAME:btnSendPNR]
    Sleep    2
    Run Keyword If    '${gds_switch}' == 'amadeus'    Handle Parallel Process    ${keyword}
    Wait Until Progress Info is Completed
    Get PNR Record Locator
    Verify PNR Message
    Set Test Variable    ${bln_CoordsKnown}    False
    ${pnr_created_folder} =    Set Variable If    '${test_environment}' == 'local'    C:\\Users\\Public\\Documents\\    '${test_environment}' == 'citrix'    D:\\TFS\\
    ${date_today} =    ExtendedCustomSyExLibrary.Get Current Date
    Run Keyword And Continue On Failure    Append To File    ${pnr_created_folder}pnr_created.txt    ${\n}${current_pnr} - ${GDS_switch} - ${date_today}
    Sleep    ${delay_in_secs}
    Run Keyword If    '${gds_switch}' == 'amadeus'    Run Keywords    Activate Amadeus Selling Platform    False
    ...    AND    Activate Power Express Window
    Log    ${current_pnr}
    [Teardown]    Take Screenshot

Click Finish PNR Expecting Popup Message
    [Arguments]    ${expected_popup_message}
    Click Panel    Recap
    Click Control Button    [NAME:btnSendPNR]
    Sleep    10
    Wait Until Window Exists    Power Express
    ${actual_popup_message}    Get Control Text Value    [NAME:txtMessageTextBox]    Power Express
    Verify Actual Value Matches Expected Value    ${actual_popup_message}    ${expected_popup_message}
    Take Screenshot
    Wait Until Keyword Succeeds    60    1    Verify Progress Info Window is Not Active
    Verify PNR Message
    Get PNR Record Locator
    ${pnr_created_folder} =    Set Variable If    '${test_environment}' == 'local'    C:\\Users\\Public\\Documents\\    '${test_environment}' == 'citrix'    D:\\TFS\\
    ${date_today} =    ExtendedCustomSyExLibrary.Get Current Date
    Run Keyword And Continue On Failure    Append To File    ${pnr_created_folder}pnr_created.txt    ${\n}${current_pnr} - ${GDS_switch} - ${date_today}
    [Teardown]    Take Screenshot

Click Finish PNR With Timestamp
    [Arguments]    ${error_message}=${EMPTY}
    Click Panel    Recap
    Click Control Button    [NAME:btnSendPNR]
    Wait Until Progress Info is Completed    ${EMPTY}    ${error_message}
    ${exp_overalltransaction_end_time}    Get Time
    Set Test Variable    ${exp_overalltransaction_end_time}
    Get PNR Record Locator
    Verify PNR Message
    Run Keyword If    '${simultaneous_changes}' != 'True' and '${retry_popup_status}' != 'True' and '${error_message}' != '${EMPTY}'    Run Keyword And Continue On Failure    FAIL    Retry error popup is not displayed.
    Set Test Variable    ${bln_CoordsKnown}    False
    ${pnr_created_folder} =    Set Variable If    '${test_environment}' == 'local'    C:\\Users\\Public\\Documents\\    '${test_environment}' == 'citrix'    D:\\TFS\\
    Run Keyword And Continue On Failure    Append To File    ${pnr_created_folder}pnr_created.txt    ${\n}${current_pnr} - ${GDS_switch}
    [Teardown]    Take Screenshot

Create Queue Minder
    [Arguments]    ${queue_pcc}    ${queue_number}    ${queue_category}    ${queue_message}
    Click Add Queue Minder
    Tick Queue Minder Date Checkbox
    Set Queue Minder PCC    ${queue_pcc}
    Set Queue Minder Number    ${queue_number}
    Set Queue Minder Category    ${queue_category}
    Set Queue Minder Message    ${queue_message}
    [Teardown]    Take Screenshot

Handle Parallel Process
    [Arguments]    ${keyword}=${EMPTY}
    Sleep    5
    ${active_window} =    Win Get Title    [ACTIVE]    ${EMPTY}
    Set Test Variable    ${popup_window}    ${active_window}
    Win Activate    ${active_window}    ${EMPTY}
    ${is_parallel_process_message_present} =    Control Command    ${active_window}    ${EMPTY}    [NAME:txtMessageTextBox]    IsVisible    ${EMPTY}
    Run Keyword If    ${is_parallel_process_message_present} == 1    Take Screenshot
    ${actual_warning_message} =    Run Keyword If    ${is_parallel_process_message_present} == 1    Get Control Text Value    [NAME:txtMessageTextBox]    ${title_power_express}
    ${parallel_process1}    Run Keyword If    ${is_parallel_process_message_present} == 1    Run Keyword And Return Status    Should Contain    ${actual_warning_message.lower()}    parallel
    ${parallel_process2}    Run Keyword If    ${is_parallel_process_message_present} == 1    Run Keyword And Return Status    Should Contain    ${actual_warning_message.lower()}    modififications
    Run Keyword If    ${parallel_process1} == True or ${parallel_process2} == True    Confirm Popup Window
    Run Keyword If    ${parallel_process1} == True or ${parallel_process2} == True    Run Keyword    ${keyword}

Populate Recap Panel With Default Values
    ${is_offline_request_number_present} =    Control Command    ${title_power_express}    ${EMPTY}    ${check_box_OfflineRequestNumber}    IsVisible    ${EMPTY}
    Run Keyword If    ${is_offline_request_number_present} == 1    Tick Disable Offline Request Number
    ...    ELSE    Select Booking Method Using Default Value
    [Teardown]    Take Screenshot

Select Booking Method
    [Arguments]    ${booking_method_value}
    Set Control Text Value    ${combo_bookingmethod}    ${booking_method_value}    ${title_power_express}

Select Booking Method Using Default Value
    Click Control Button    ${combo_bookingmethod}    ${title_power_express}
    Control Focus    ${title_power_express}    ${EMPTY}    ${combo_bookingmethod}
    Send    {DOWN}
    Send    {TAB}

Send Itinerary
    Retrieve PNR    ${current_pnr}
    Click Send Itinerary
    Click Finish PNR

Set Clipper Compliancy Value
    [Arguments]    ${str_clipper_compliancy}
    Sleep    5
    Control Click    ${title_power_express}    ${EMPTY}    ${clipper_compliancy_options}
    Sleep    1
    Send    ${str_clipper_compliancy}    0
    Sleep    1
    Send    {ENTER}
    Sleep    1
    ${ui_clipper_compliancy} =    Control Get Text    ${title_power_express}    ${EMPTY}    ${clipper_compliancy_options}
    ${match_found} =    Run Keyword and Return Status    Should Match    ${ui_clipper_compliancy}    ${str_clipper_compliancy}
    Run Keyword If    "${match_found}" == "False"    Run Keyword And Continue On Failure    Fail    Failed to enter new clipper compliancy value
    Take Screenshot
    Sleep    5
    Control Click    ${title_power_express}    ${EMPTY}    ${edit_pcc}
    Send    {TAB}

Set PCC Field Value
    [Arguments]    ${str_pcc}
    Sleep    5
    Control Set Text    ${title_power_express}    ${EMPTY}    ${edit_pcc}    ${str_pcc}
    Sleep    1
    [Teardown]    Take Screenshot

Set Queue Minder Category
    [Arguments]    ${queue_category}
    Set Control Text Value    [NAME:ctxtCategory0]    ${queue_category}

Set Queue Minder Message
    [Arguments]    ${queue_message}
    Set Control Text Value    [NAME:ctxtMessage0]    ${queue_message}

Set Queue Minder Number
    [Arguments]    ${queue_number}
    Set Control Text Value    [NAME:ctxtQueue0]    ${queue_number}

Set Queue Minder PCC
    [Arguments]    ${queue_pcc}
    Set Control Text Value    [NAME:ctxtPCC0]    ${queue_pcc}

Set Skip Entries
    [Arguments]    ${skip_entries_value}
    Set Control Text Value    [NAME:ccboSkipEntries]    ${skip_entries_value}

Set Team ID Field Value
    [Arguments]    ${str_teamid}
    Sleep    5
    Control Set Text    ${title_power_express}    ${EMPTY}    ${edit_teamid}    ${str_teamid}
    Sleep    1
    [Teardown]    Take Screenshot

Tick Auto Invoice
    Activate Power Express Window
    Tick Checkbox Via Description    Auto Invoice

Tick Disable Offline Request Number
    Click Control Button    ${check_box_OfflineRequestNumber}    ${title_power_express}

Tick Queue Minder Date Checkbox
    Tick Checkbox    [NAME:cdtpDate0]    By Space
    ${queue_date}    Get Control Text Value    [NAME:cdtpDate0]
    Set Suite Variable    ${queue_date}

Untick Auto Invoice
    Activate Power Express Window
    Untick Checkbox Via Description    Auto Invoice
