*** Settings ***
Documentation     This resource file covers all reusable actions for Air Fare Related test cases
Resource          ../common/utilities.txt
Variables         ../variables/contact_details_control_objects.py
Resource          id_traveller.txt

*** Keywords ***
Click Tab In Contact Details
    [Arguments]    ${tab_name}
    Mouse Click    LEFT    585    65
    Click Control Button    [NAME:ContactDetailsTabControl]
    Select Tab Control    ${tab_name}

Handle Incomplete Contact Details
    [Arguments]    ${client_account}=${EMPTY}
    ${client_account_value} =    Get Control Text Value    ${combobox_account_number}    ${title_power_express}
    ${does_client_account_contains_preset_value} =    Run Keyword And Return Status    Should Match Regexp    ${client_account_value}    Choose|Ausw|Select
    Run Keyword If    ${does_client_account_contains_preset_value} == True and '${client_account}' =='${EMPTY}'    Select Client Account Using Default Value
    ...    ELSE    Select Client Account Value    ${client_account}
    ${telephone_value} =    Get Control Text Value    ${textbox_telephone}    ${title_power_express}
    ${mobile_value} =    Get Control Text Value    ${textbox_mobile}    ${title_power_express}
    ${telephone_length} =    Get Length    ${telephone_value}
    ${mobile_length} =    Get Length    ${mobile_value}
    ${is_telephone_format_correct} =    Run Keyword And Return Status    Should Match Regexp    ${telephone_value}    \\d*-\\d*-\\d.*
    ${is_mobile_format_correct} =    Run Keyword And Return Status    Should Match Regexp    ${mobile_value}    \\d*-\\d*-\\d.*
    Run Keyword If    ${telephone_length} == 0 and ${mobile_length} == 0    Run Keywords    Set Control Text Value    ${textbox_telephone}    11-22-33    ${title_power_express}
    ...    AND    Send    {TAB}
    Run Keyword If    ${is_telephone_format_correct} == False and ${telephone_length} > 0    Run Keywords    Set Control Text Value    ${textbox_telephone}    ${telephone_value}-123    ${title_power_express}
    ...    AND    Send    {TAB}
    Run Keyword If    ${is_mobile_format_correct} == False and ${mobile_length} > 0    Run Keywords    Set Control Text Value    ${textbox_mobile}    ${mobile_value}-123    ${title_power_express}
    ...    AND    Send    {TAB}
    ${traveller_checkbox_status} =    Get Checkbox Status    ${checkbox_traveller}
    ${arranger_checkbox_status} =    Get Checkbox Status    ${checkbox_arranger}
    Run Keyword Unless    ${traveller_checkbox_status} == True    Control Click    ${title_power_express}    ${EMPTY}    ${checkbox_traveller}
    Run Keyword Unless    ${arranger_checkbox_status} == True    Control Click    ${title_power_express}    ${EMPTY}    ${checkbox_arranger}

Populate Caller Tab
    [Arguments]    ${firstname}    ${lastname}    ${area_code}    ${phone_number}    ${email}
    Set First name In Contact    ${firstname}
    Set Last Name In Contact    ${lastname}
    Set Area Code    ${area_code}
    Set Phone Number    ${phone_number}
    Set Email Address    ${email}

Select Client Account
    [Arguments]    ${client_account_value}
    Select Value From Combobox    [NAME:ccboAccountNumber]    ${client_account_value}

Select Client Account Using Default Value
    Click Control Button    ${combobox_account_number}    ${title_power_express}
    Control Focus    ${title_power_express}    ${EMPTY}    ${combobox_account_number}
    Send    {DOWN}    0
    Sleep    0.5
    Send    {ENTER}

Select Client Account Value
    [Arguments]    ${client_account}
    Wait Until Control Object Is Enabled    ${combobox_account_number}    \    True
    Select Value From Dropdown List    ${combobox_account_number}    ${client_account}

Select Linked Contact
    Click Panel    ID Traveller
    Send    {RIGHT}{SPACE}

Select Trip Type Value
    [Arguments]    ${trip_type}
    Select Value From Dropdown List    ${combobox_trip_type}    ${trip_type}
    Take Screenshot

Set Area Code
    [Arguments]    ${area_code}=12
    Set Control Text Value    [NAME:ctxtAreaCode]    ${area_code}
    Send    {TAB}

Set Date Of Birth
    [Arguments]    ${birthdate}
    @{birthdate}    Split String    ${birthdate}    /
    Click Control Button    [NAME:dtpDate]    ${title_power_express}
    Send    ${birthdate[2]}    1
    Send    {LEFT}
    Send    ${birthdate[0]}    1
    Send    {LEFT}
    Send    ${birthdate[1]}    1
    Sleep    1
    Send    {TAB}
    Sleep    1
    [Teardown]    Take Screenshot

Set Email Address
    [Arguments]    ${email}=automation@carlsonwagonlit.com
    Set Control Text Value    [NAME:ctxtEmail]    ${email}

Set First name In Contact
    [Arguments]    ${firstname}
    Set Control Text Value    [NAME:ctxtFirstName]    ${firstname}
    Send    {TAB}

Set Last Name In Contact
    [Arguments]    ${lastname}
    Set Control Text Value    [NAME:ctxtLastName]    ${lastname}
    Send    {TAB}

Set Mobile Number
    [Arguments]    ${mobile_value}=12-23-34
    Set Control Text Value    ${textbox_mobile}    ${mobile_value}    ${title_power_express}
    Send    {TAB}

Set Phone Number
    [Arguments]    ${phone_number}=34567
    Set Control Text Value    [NAME:ctxtPhoneNumber]    ${phone_number}
    Send    {TAB}

Set Telephone
    [Arguments]    ${phone_value}=12-23-34
    Set Control Text Value    ${textbox_telephone}    ${phone_value}    ${title_power_express}
    Send    {TAB}

Tick Contact Checkbox
    Tick Checkbox via Coordinates    ${checkbox_arranger}

Tick Sponsor Checkbox
    Tick Checkbox via Coordinates    ${checkbox_sponsor}

Tick Traveller Checkbox
    Tick Checkbox via Coordinates    ${checkbox_traveller}

Untick Contact Checkbox
    Untick Checkbox via Coordinates    ${checkbox_arranger}

Untick Sponsor Checkbox
    Untick Checkbox via Coordinates    ${checkbox_sponsor}

Untick Traveller Checkbox
    Untick Checkbox via Coordinates    ${checkbox_traveller}
