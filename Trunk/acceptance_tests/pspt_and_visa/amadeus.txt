*** Settings ***
Force Tags        amadeus    emea
Resource          pspt_and_visa_verification.txt

*** Test Cases ***
[1A EMEA][NB] Verify That Pspt & Visa And APIS/SFPD Panels Are Shown For FR Non-Mindef
    [Tags]    us3575    fr
    Open Power Express And Retrieve Profile    ${version}    Test    U003WXR    fr-FR    AutomationFR    FR AUTO AMADEUS
    Select GDS    Amadeus
    Create New Booking With One Way Flight And One Alternate Offer Using Default Values    Amadeus FR Legacy - Auto ¦ US681    AutoLegacy    EmeaTrvlrOneFive    LAXCDG/AAF    SS1Y1    FXB
    ...    ${EMPTY}    AIR FARE
    Verify Pspt & Visa And APIS/SFPD Panels Are Shown For Non-Mindef
    [Teardown]    Click Clear All

[NB] Verify That Domestic Trip Checkbox Is Unchecked For Domestic Flight Different From Traveler's Nationality Timatic Off
    [Tags]    us5155    US5695    uk
    Open Power Express And Retrieve Profile    ${version}    Test    U003WXR    en-GB    AutomationUK    UK AUTO AMADEUS
    Select GDS    Amadeus
    Create New Booking With Round Trip Air Flight Using Default Values    UK Amadeus 1 - SyEx ¦ Power Hotel Integration    Edwards    Cory    YYZYVR/AAC    SS1Y1    ${EMPTY}
    ...    YVRYYZ/AAC    SS1Y1    FXP    ${EMPTY}    Pspt and Visa
    Click Panel    Pspt and Visa
    Verify Domestic Trip Checkbox Is Unticked
    Verify Passport & Visa Info Panel Is Not Displayed
    Click Check Visa Requirements
    Verify Travel Document Details Are Correct    British Citizen passport    United Kingdom    A09876543    10/30/2020    Yes
    Verify Countries Visited    Canada
    Tick Use Document
    Click Check Visa Requirements No ESTA
    Get Passport & Visa Info Panel Text
    Verify Pspt And Visa Info Panel Contains Text    must have an Electronic Travel Authorization (eTA)
    Click Finish PNR
    Retrieve PNR Details From Amadeus    ${current_pnr}
    Verify Specific Line Is Written In The PNR    RIR TRAVELLERS NATIONALITY: UNITED KINGDOM
    Verify Specific Line Is Written In The PNR    A09876543
    Verify Specific Line Is Written In The PNR    10/30/2020
    [Teardown]

[AB] Verify That Domestic Trip Checkbox Is Unchecked For Domestic Flight Different From Traveler's Nationality Timatic Off
    [Tags]    us5155    US5695    uk
    Retrieve PNR    ${current_pnr}
    Click Amend Booking
    Book Flight X Months From Now    DENBOS    SS1Y1    FXP/S4    6    10
    Click Read Booking
    Populate All Panels (Except Given Panels If Any)    Pspt and Visa
    Click Panel    Pspt and Visa
    Verify Checkbox Is Unticked    ${check_box_domestic_trip}
    Verify Passport & Visa Info Panel Is Not Displayed
    Verify Travel Document Details Are Correct    British Citizen passport    United Kingdom    A09876543    10/30/2020    Yes
    Verify Countries Visited    Canada    United States
    Tick Use Document
    Click Check Visa Requirements No ESTA
    Get Passport & Visa Info Panel Text
    Verify Pspt And Visa Info Panel Contains Text    must have an Electronic Travel Authorization (eTA)
    Click Finish PNR    Amend Booking Workflow For Domestic Trip Checkbox Unticked Timatic Off
    Execute Simultaneous Change Handling    Amend Booking Workflow For Domestic Trip Checkbox Unticked Timatic Off
    Retrieve PNR Details From Amadeus    ${current_pnr}
    Verify Specific Line Is Written In The PNR    RIR TRAVELLERS NATIONALITY: UNITED KINGDOM
    Verify Specific Line Is Written In The PNR    A09876543
    Verify Specific Line Is Written In The PNR    10/30/2020
    [Teardown]    Cancel PNR Thru GDS Native    ${current_pnr}

[1A EMEA][NB] Verify Countries Are Captured With Correct Visa Information In Domestic RT Flight With Diff Nationality Docs For Timatic On
    [Tags]    US5337    US5696    US5695    US3844    DE6819    fr
    Open Power Express And Retrieve Profile    ${version}    Test    U003WXR    fr-FR    AutomationFR    FR AUTO AMADEUS
    Select GDS    Amadeus
    Create New Booking With Round Trip Air Flight Using Default Values    Amadeus FR 16 - Auto ¦ US1423    AutoSixteenTwo    NoramTrvlrFive    NCEORY    SS1Y1    ${EMPTY}
    ...    ORYNCE    SS1Y1    FXP    ${EMPTY}    Pspt and Visa
    Click Panel    Pspt and Visa
    Verify Domestic Trip Checkbox Is Ticked
    Untick Domestic Trip
    Populate Pspt And Visa With Values    Passport: Normal    Philippines    EB0987654    12/20/2020    ${EMPTY}
    Click Check Visa Requirements No ESTA
    Verify Travel Document Details Are Correct    Passport: Normal    Philippines    EB0987654    12/20/2020    Oui
    Get Passport & Visa Info Panel Text
    Verify That Countries Visited And Visa Status Is Correct    France    Obligatoire
    Populate All Panels (Except Given Panels If Any)    Pspt and Visa
    Click Finish PNR
    Retrieve PNR Details From Amadeus    ${current_pnr}
    Verify Specific Line Is Written In The PNR    NATIONALITE DU VOYAGEUR: PH
    Comment    Verify Specific Line Is Written In The PNR    VALIDITE DU PASSEPORT: NON
    Verify Specific Line Is Written In The PNR    CONTROLE DE VISA: FRANCE OBLIGATOIRE

[1A EMEA][AB] Verify Countries Are Captured With Correct Visa Information In International Multi Flight With Diff Nationality Docs For Timatic On
    [Tags]    US5337    US5696    US5695    US3844    DE6819    fr
    Retrieve PNR    ${current_pnr}
    Click Amend Booking
    Enter Command In Native GDS    XI    XT
    Book Flight X Months From Now    TLSNCE    SS1Y2    ${EMPTY}    5    1
    Book Flight X Months From Now    NCETLS    SS1Y3    ${EMPTY}    5    5
    Book Flight X Months From Now    FRAMUC    SS1Y1    ${EMPTY}    5    8
    Book Flight X Months From Now    MUCFRA    SS1Y1    ${EMPTY}    5    13
    Book Flight X Months From Now    MNLSIN    SS1Y1    ${EMPTY}    5    20
    Book Flight X Months From Now    JFKMUC    SS1Y1    ${EMPTY}    5    25
    Click Read Booking
    Click Panel    Pspt and Visa
    Verify Checkbox Is Unticked    ${check_box_domestic_trip}
    Populate Pspt And Visa With Values    Passport: Normal    Philippines    EB0987654    12/20/2020    ${EMPTY}
    Click Check Visa Requirements No ESTA
    Verify Passport & Visa Info Panel Is Displayed
    Verify Travel Document Details Are Correct    Passport: Normal    Philippines    EB0987654    12/20/2020    Oui
    Verify That Countries Visited And Visa Status Is Correct    France    Obligatoire
    Verify That Countries Visited And Visa Status Is Correct    Allemagne    Obligatoire
    Verify That Countries Visited And Visa Status Is Correct    Etats-Unis    Obligatoire
    [Teardown]    Cancel PNR Thru GDS Native    ${current_pnr}

*** Keywords ***
Amend Booking Workflow For Domestic Trip Checkbox Unticked Timatic Off
    Retrieve PNR    ${current_pnr}
    Click Amend Booking
    Book Flight X Months From Now    DENBOS    SS1Y1    FXP/S4    6    10
    Click Read Booking
    Populate All Panels (Except Given Panels If Any)    Pspt and Visa
    Click Panel    Pspt and Visa
    Verify Checkbox Is Unticked    ${check_box_domestic_trip}
    Verify Passport & Visa Info Panel Is Not Displayed
    Verify Travel Document Details Are Correct    British Citizen passport    United Kingdom    A09876543    10/30/2020    Yes
    Verify Countries Visited    Canada    United States
    Tick Use Document
    Click Check Visa Requirements No ESTA
    Get Passport & Visa Info Panel Text
    Verify Pspt And Visa Info Panel Contains Text    must have an Electronic Travel Authorization (eTA)
    Click Finish PNR    Amend Booking Workflow For Domestic Trip Checkbox Unticked Timatic Off

Amend Booking Workflow For Domestic Trip Checkbox Same Nationality
    Retrieve PNR    ${current_pnr}
    Click Amend Booking
    Click Read Booking
    Populate All Panels (Except Given Panels If Any)    Pspt and Visa
    Click Panel    Pspt and Visa
    Verify Checkbox Is Ticked    ${check_box_domestic_trip}
    Verify Passport & Visa Info Panel Is Not Displayed
    Click Finish PNR    Amend Booking Workflow For Domestic Trip Checkbox Same Nationality
