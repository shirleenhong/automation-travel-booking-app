*** Settings ***
Force Tags        amadeus    emea
Resource          ../delivery_verification.txt
Resource          ../../third_party/mindef/mindef_verification.txt

*** Test Cases ***
[NB DE] Verify That Quick Steps Is Working For 'On-Hold - Awaiting Customer References'
    [Tags]    us2444    de
    Open Power Express And Retrieve Profile    ${version}    Test    U003WXR    de-DE    AutomationDE    DE AUTO AMADEUS
    Select GDS    Amadeus
    Create New Booking With Round Trip Air Flight Using Default Values    Amadeus DE 15 - Auto ¦ US2447    AutoSeventeenFive    PerfEight    LHRCDG/AAF    SS1Y1    ${EMPTY}
    ...    CDGLHR/AAF    SS1Y1    FXB    ${EMPTY}    Air Fare    Cust Refs
    ...    Delivery
    Click Panel    Cust Refs
    Tick Not Known At Time Of Booking    TICK
    Click Panel    Air Fare
    Populate Fare Tab With Default Values    Fare 1
    Click Panel    Delivery
    Populate Delivery Panel Without On Hold Reason
    Verify On Hold Booking Reason Is Checked    Awaiting Customer References
    Verify On Hold Booking Reason Is Disabled    Awaiting Customer References
    Click Finish PNR
    Retrieve PNR Details from Amadeus    ${current_pnr}
    Verify Specific Line Is Written In The PNR    RMM ONHOLD:FEHLENDE ZUSATZDATEN
    Click Clear All With Options    Clear All

[QA DE] Verify That Quick Steps Is Working For 'On-Hold - Awaiting Customer References'
    [Tags]    us2444    de
    Retrieve PNR    ${current_pnr}
    Verify Quick Amend Is Visible
    Verify Quick Amend Spiel Is Visible    Diese Buchung ist auf Status ‘Warten auf Kundenreferenzen‘, sparen sie Zeit und benutzen sie den ‚Quick Amend‘ Knopf um fehlende Daten einzugeben und die Buchung abzuschließen!
    Delete Current Specific Log File    SyExPerfLog
    Click Quick Amend With Timestamp
    Verify Actual Panel Equals To Expected Panel    CUST REFS    DELIVERY    RECAP
    Click Panel    Cust Refs
    Verify Cust Refs Not Known Checkbox Is Unticked And Disabled
    Click Panel    Delivery
    Verify Reasons For Placing Booking On Hold Section In Delivery Panel Is Disabled
    Verify On Hold Booking Reason Is Unchecked    Awaiting Customer References
    Populate Delivery Panel Without On Hold Reason
    Click Finish PNR    Quick Amend
    Re-Execute Quick Amend If Simultaneous Change Error Is Displayed
    Retrieve PNR Details from Amadeus    ${current_pnr}
    Verify Specific Line Is Not Written In The PNR    RMM ONHOLD:FEHLENDE ZUSATZDATEN
    Verify Entry Is Present In Perf Log File    BusinessFunctionMetric.AmendBookingAddCDR
    Verify Entry Is Present In Application Usage DB    AmendBookingAddCDR    ${exp_OverallTransactionStartTime}
    Click Clear All With Options    Clear All
    Retrieve PNR    ${current_pnr}
    Verify Quick Amend Is Not Visible
    [Teardown]    Cancel PNR Thru GDS Native    ${current_pnr}

[NB DE] Verify That Quick Steps Is Working For 'On-Hold - Awaiting Approval'
    [Tags]    us2447    de
    Open Power Express And Retrieve Profile    ${version}    Test    U003WXR    de-DE    AutomationDE    DE AUTO AMADEUS
    Select GDS    Amadeus
    Create New Booking With One Way Flight Using Default Values    Amadeus DE 15 - Auto ¦ US2447    AutoSeventeenFive    PerfEight    LHRCDG/AAF    ${EMPTY}    AIR FARE
    ...    CUST REFS
    Populate Fare Tab With Default Values    Fare 1
    Click Finish PNR
    Retrieve PNR Details From Amadeus    ${current_pnr}
    Verify Specific Line Is Written In The PNR    RMM ONHOLD:FEHLENDE GENEHMIGUNG
    Click Clear All With Options    Clear All

[QA DE] Verify That Quick Steps Is Working For 'On-Hold - Awaiting Approval'
    [Tags]    us2447    de
    Retrieve PNR    ${current_pnr}
    Verify Quick Amend Is Visible
    Verify Quick Amend Spiel Is Visible    Diese Buchung ist auf Status ‘Warten auf Freigabe‘, sparen sie Zeit und benutzen sie den ‚Quick Amend‘ Knopf um fehlende Daten einzugeben und die Buchung abzuschließen!
    Delete Current Specific Log File    SyExPerfLog
    Click Quick Amend With Timestamp
    Verify Actual Panel Equals To Expected Panel    DELIVERY    RECAP
    Click Panel    Delivery
    Verify Reasons For Placing Booking On Hold Section In Delivery panel Is Disabled
    Verify On Hold Booking Reason Is Unchecked    Awaiting Approval
    Populate Delivery Panel Without On Hold Reason
    Click Finish PNR    Quick Amend
    Re-Execute Quick Amend If Simultaneous Change Error Is Displayed
    Retrieve PNR Details from Amadeus    ${current_pnr}
    Verify Specific Line Is not Written In The PNR    RMM ONHOLD:FEHLENDE GENEHMIGUNG
    Verify Entry is Present in Perf Log File    BusinessFunctionMetric.AmendBookingAddSimpleApproval
    Verify Entry is Present in Application Usage DB    AmendBookingAddSimpleApproval    ${exp_OverallTransactionStartTime}
    Click Clear All With Options    Clear All
    Retrieve PNR    ${current_pnr}
    Verify Quick Amend Is Not Visible
    [Teardown]    Cancel PNR Thru GDS Native    ${current_pnr}

[NB UK] Verify That Quick Steps Is Working For 'On-Hold - Awaiting Secure Flight Data'
    [Tags]    us2445    uk
    Open Power Express And Retrieve Profile    ${version}    Test    U003WXR    en-GB    AutomationUK    UK AUTO AMADEUS
    Select GDS    Amadeus
    Create New Booking With One Way Flight Using Default Values    Amadeus UK 16 - Auto ¦ US1641    AutoOneSixSeven    EmeaTrvlrFour    MSPYYZ/AUA    ${EMPTY}    Air Fare
    ...    APIS/SFPD    Delivery    Cust Refs
    Click Panel    Cust Refs
    Set CDR Value    TRVLCOST    TEST
    Click Panel    Air Fare
    Populate Fare Tab With Default Values    Fare 1
    Click Panel    APIS/SFPD
    Populate APIS/SFPD Panel With Default Values
    Click Panel    Delivery
    Populate Delivery Panel Without On Hold Reason
    Verify On Hold Booking Reason Is Checked    Awaiting Secure Flight Data
    Verify On Hold Booking Reason Is Disabled    Awaiting Secure Flight Data
    Click Finish PNR
    Retrieve PNR Details From Amadeus    ${current_pnr}
    Verify Specific Line Is Written In The PNR    RMM ONHOLD:AWAITING SECURE FLIGHT DATA

[QA UK] Verify That Quick Steps Is Working For 'On-Hold - Awaiting Secure Flight Data'
    [Tags]    us2445    uk
    Retrieve PNR    ${current_pnr}
    Verify Quick Amend Is Visible
    Verify Quick Amend Spiel Is Visible    This booking is ‘On Hold – Awaiting SFPD’, save time by using the ‘Quick Amend’ button to add the missing data and finish the booking!
    Delete Current Specific Log File    SyExPerfLog
    Click Quick Amend With Timestamp
    Verify Actual Panel Equals To Expected Panel    APIS/SFPD    DELIVERY    RECAP
    Click Panel    APIS/SFPD
    Verify APIS/SFPD Not Known Checkbox Is Unticked And Disabled
    Click Panel    Delivery
    Verify Reasons For Placing Booking On Hold Section In Delivery panel Is Disabled
    Verify On Hold Booking Reason Is Unchecked    Awaiting Secure Flight Data
    Populate Delivery Panel Without On Hold Reason
    Click Finish PNR    Quick Amend
    Re-Execute Quick Amend If Simultaneous Change Error Is Displayed
    Retrieve PNR Details From Amadeus    ${current_pnr}
    Verify Specific Line Is Not Written In The PNR    RMM ONHOLD:AWAITING SECURE FLIGHT DATA
    Verify Entry Is Present In Perf Log File    BusinessFunctionMetric.AmendBookingAddSFPD
    Verify Entry Is Present In Application Usage DB    AmendBookingAddSFPD    ${exp_OverallTransactionStartTime}
    Retrieve PNR    ${current_pnr}
    Verify Quick Amend Is Not Visible
    [Teardown]    Cancel PNR Thru GDS Native    ${current_pnr}

[NB DE] Verify That Quick Steps Is Working For 'On-Hold - Awaiting Policy Check'
    [Tags]    us2446    de
    Open Power Express And Retrieve Profile    ${version}    Test    U003WXR    de-DE    AutomationDE    DE AUTO AMADEUS
    Select GDS    Amadeus
    Create New Booking With Round Trip Air Flight Using Default Values    Amadeus DE 15 - Auto ¦ US2447    AutoSeventeenFive    PerfEight    BNESYD/AVA    SS1Y1    ${EMPTY}
    ...    SYDBNE/AVA    SS1Y1    FXB    ${EMPTY}    AIR FARE    CUST REFS
    ...    POLICY CHECK    DELIVERY    CLIENT INFO
    Click Panel    Client Info
    Untick Not Known At Time Of Booking For Form Of Payment
    Select Form Of Payment    Cash
    Click Panel    Air Fare
    Populate Fare Tab With Default Values    Fare 1
    Click Panel    Policy Check
    Select Policy Status    Australia Risk    HA - darf reisen
    Select Policy Status    Virgin Australia Risk    AA - Genehmigung abwarten
    Click Panel    Delivery
    Populate Delivery Panel Without On Hold Reason
    Verify On Hold Booking Reason Is Checked    Awaiting Approval
    Verify On Hold Booking Reason Is Disabled    Awaiting Approval
    Click Finish PNR
    Retrieve PNR Details From Amadeus    ${current_pnr}
    Verify Specific Line Is Written In The PNR    RMM ONHOLD:FEHLENDE GENEHMIGUNG
    Click Clear All With Options    Clear All

[QA DE] Verify That Quick Steps Is Working For 'On-Hold - Awaiting Policy Check'
    [Tags]    us2446    de
    Retrieve PNR    ${current_pnr}    10
    Verify Quick Amend Is Visible
    Verify Quick Amend Spiel Is Visible    Diese Buchung ist auf Status ‚Reiserichtliniencheck - warten auf Freigabe‘, sparen sie Zeit und benutzen sie den ‚Quick Amend‘ Knopf um fehlende Daten einzugeben und die Buchung abzuschließen!
    Delete Current Specific Log File    SyExPerfLog
    Click Quick Amend With Timestamp
    Verify Actual Panel Equals To Expected Panel    POLICY CHECK    DELIVERY    RECAP
    Click Panel    Policy Check
    Verify Policy Status Is Blank By Default    Virgin Australia Risk
    Verify Policy Status Does Not Contain Option    Virgin Australia Risk    AA - Genehmigung abwarten
    Verify Policy Reason Is Not Displayed    Australia Risk
    Select Policy Status    Virgin Australia Risk    HA - darf reisen
    Click Panel    Delivery
    Verify Reasons For Placing Booking On Hold Section In Delivery panel Is Disabled
    Verify On Hold Booking Reason Is Unchecked    Awaiting Approval
    Populate Delivery Panel Without On Hold Reason
    Click Finish PNR    Quick Amend For Awaiting Policy Check
    Execute Simultaneous Change Handling    Quick Amend For Awaiting Policy Check
    Retrieve PNR Details From Amadeus    ${current_pnr}
    Verify Specific Line Is Not Written In The PNR    RMM ONHOLD:FEHLENDE GENEHMIGUNG
    Verify Entry Is Present In Perf Log File    BusinessFunctionMetric.AmendBookingAddPolicyApproval
    Verify Entry Is Present In Application Usage DB    AmendBookingAddPolicyApproval    ${exp_OverallTransactionStartTime}
    Click Clear All With Options    Clear All
    Retrieve PNR    ${current_pnr}
    Verify Quick Amend Is Not Visible
    [Teardown]    Cancel PNR Thru GDS Native    ${current_pnr}

[QA FR] Verify That RMM Remarks Are Written When Awaiting Cust Reference Is Selected
    [Tags]    mindef    us5497    fr
    Create Mindef PNR
    Retrieve PNR    ${current_pnr}
    Click Amend Booking
    Book Flight X Months From Now With Requested Airline Without Pricing    LHRCDG/AAF    SS1Y1
    GDS Pricing Per Pax Type    S3    P1    0    0
    Click Read Booking
    Click Panel    Policy Check
    Select Policy Status    Nécessite une validation avant émission    NV - Ne se rend pas dans la zone affectée
    Click Panel    Cust Refs
    Tick Not Known At Time Of Booking    TICK
    Click Panel    Air Fare
    Populate Fare Tab With Default Values    Fare 1
    Click Panel    Delivery
    Populate Delivery Panel Without On Hold Reason
    Verify On Hold Booking Reason Is Checked    Awaiting Customer References
    Verify On Hold Booking Reason Is Disabled    Awaiting Customer References
    Click Finish PNR    Amend Booking Where Awaiting Cust Reference Is Selected
    Execute Simultaneous Change Handling    Amend Booking Where Awaiting Cust Reference Is Selected
    Retrieve PNR Details from Amadeus    ${current_pnr}
    Verify Specific Line Is Written In The PNR    RMM LASTUSER-U003WXR-AUTOMATIONFR    \    true
    Verify Specific Line Is Written In The PNR    RMM ONHOLD:EN ATTENTE REFERENCES CLIENTS    \    true
    Verify Specific Line Is Written In The PNR    RM *0111*2020    \    true
    Retrieve PNR    ${current_pnr}
    Verify Quick Amend Is Visible
    [Teardown]    Cancel PNR Thru GDS Native    ${current_pnr}

[QA FR] Verify That RMM Remarks Are Written When Awaiting Approval Is Selected
    [Tags]    us5497    mindef    fr
    Create Mindef PNR
    Retrieve PNR    ${current_pnr}
    Click Amend Booking
    Book Flight X Months From Now With Requested Airline Without Pricing    LHRCDG/AAF    SS1Y1
    GDS Pricing Per Pax Type    S3    P1    0    0
    Click Read Booking
    Click Panel    Policy Check
    Select Policy Status    Nécessite une validation avant émission    NV - Ne se rend pas dans la zone affectée
    Click Panel    Air Fare
    Populate Fare Tab With Default Values    Fare 1
    Click Panel    Delivery
    Populate Delivery Panel With Default Values
    Verify On Hold Booking Reason Is Checked    Awaiting Approval
    Click Finish PNR    Amend Booking Where Awaiting Approval Is Selected
    Execute Simultaneous Change Handling    Amend Booking Where Awaiting Approval Is Selected
    Retrieve PNR Details from Amadeus    ${current_pnr}
    Verify Specific Line Is Written In The PNR    RMM LASTUSER-U003WXR-AUTOMATIONFR    \    true
    Verify Specific Line Is Written In The PNR    RMM ONHOLD:EN ATTENTE DE VALIDATION    \    true
    Verify Specific Line Is Written In The PNR    RM *0111*2020    \    true
    Retrieve PNR    ${current_pnr}
    Verify Quick Amend Is Visible
    [Teardown]    Cancel PNR Thru GDS Native    ${current_pnr}

[QA FR] Verify That RMM Remarks Are Written When Policy Check Awaiting Approval Is Selected
    [Tags]    us5497    mindef    fr
    Create Mindef PNR
    Retrieve PNR    ${current_pnr}
    Click Amend Booking
    Book Flight X Months From Now With Requested Airline Without Pricing    LHRCDG/AAF    SS1Y1
    GDS Pricing Per Pax Type    S3    P1    0    0
    Click Read Booking
    Click Panel    Policy Check
    Select Policy Status    Nécessite une validation avant émission    AA - En attente de validation
    Click Panel    Air Fare
    Populate Fare Tab With Default Values    Fare 1
    Click Panel    Delivery
    Populate Delivery Panel Without On Hold Reason
    Verify On Hold Booking Reason Is Checked    Awaiting Approval
    Click Finish PNR    Amend Booking Where Awaiting Secured Flight Data Is Selected
    Execute Simultaneous Change Handling    Amend Booking Where Awaiting Secured Flight Data Is Selected
    Retrieve PNR Details from Amadeus    ${current_pnr}
    Verify Specific Line Is Written In The PNR    RMM LASTUSER-U003WXR-AUTOMATIONFR    \    true
    Verify Specific Line Is Written In The PNR    RMM ONHOLD:EN ATTENTE DE VALIDATION    \    true
    Verify Specific Line Is Written In The PNR    RM *0111*2020    \    true
    Retrieve PNR    ${current_pnr}
    Verify Quick Amend Is Visible
    [Teardown]    Cancel PNR Thru GDS Native    ${current_pnr}

*** Keywords ***
Quick Amend
    Retrieve PNR    ${current_pnr}
    Click Quick Amend With Timestamp
    Click Panel    Delivery
    Populate Delivery Panel Without On Hold Reason
    Click Finish PNR    Quick Amend

Quick Amend For Awaiting Policy Check
    Retrieve PNR    ${current_pnr}    10
    Click Quick Amend With Timestamp
    Click Panel    Policy Check
    Select Policy Status    Virgin Australia Risk    HA - darf reisen
    Click Panel    Delivery
    Populate Delivery Panel Without On Hold Reason
    Click Finish PNR

Create Mindef PNR
    Open Power Express And Retrieve Profile    ${version}    Test    U003WXR    fr-FR    AutomationFR    FR QA Team Amadeus
    Select GDS    amadeus
    Create Mindef PNR In Amadeus Screen    PARWL210G    1
    Update Sub Unit GUID    GUID    GUID/CT-A:8BD9D/SU-A:8BBAF

Amend Booking Where Awaiting Cust Reference Is Selected
    Retrieve PNR    ${current_pnr}
    Click Amend Booking
    Book Flight X Months From Now With Requested Airline Without Pricing    LHRCDG/AAF    SS1Y1
    GDS Pricing Per Pax Type    S3    P1    0    0
    Click Read Booking
    Click Panel    Policy Check
    Select Policy Status    Nécessite une validation avant émission    NV - Ne se rend pas dans la zone affectée
    Click Panel    Cust Refs
    Tick Not Known At Time Of Booking    TICK
    Click Panel    Air Fare
    Populate Fare Tab With Default Values    Fare 1
    Click Panel    Delivery
    Populate Delivery Panel Without On Hold Reason
    Verify On Hold Booking Reason Is Checked    Awaiting Customer References
    Verify On Hold Booking Reason Is Disabled    Awaiting Customer References
    Click Finish PNR    Amend Booking Where Awaiting Cust Reference Is Selected

Amend Booking Where Awaiting Approval Is Selected
    Retrieve PNR    ${current_pnr}
    Click Amend Booking
    Book Flight X Months From Now With Requested Airline Without Pricing    LHRCDG/AAF    SS1Y1
    GDS Pricing Per Pax Type    S3    P1    0    0
    Click Read Booking
    Click Panel    Policy Check
    Select Policy Status    Nécessite une validation avant émission    NV - Ne se rend pas dans la zone affectée
    Click Panel    Air Fare
    Populate Fare Tab With Default Values    Fare 1
    Click Panel    Delivery
    Populate Delivery Panel With Default Values
    Verify On Hold Booking Reason Is Checked    Awaiting Approval
    Click Finish PNR    Amend Booking Where Awaiting Approval Is Selected

Amend Booking Where Awaiting Secured Flight Data Is Selected
    Create Mindef PNR
    Retrieve PNR    ${current_pnr}
    Click Amend Booking
    Book Flight X Months From Now With Requested Airline Without Pricing    LHRCDG/AAF    SS1Y1
    GDS Pricing Per Pax Type    S3    P1    0    0
    Click Read Booking
    Click Panel    Policy Check
    Select Policy Status    Nécessite une validation avant émission    AA - En attente de validation
    Click Panel    Air Fare
    Populate Fare Tab With Default Values    Fare 1
    Click Panel    Delivery
    Populate Delivery Panel Without On Hold Reason
    Verify On Hold Booking Reason Is Checked    Awaiting Approval
    Click Finish PNR    Amend Booking Where Awaiting Secured Flight Data Is Selected
